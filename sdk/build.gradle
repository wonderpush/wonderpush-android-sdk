apply plugin: 'com.android.library'

project.group = 'com.wonderpush'
project.archivesBaseName = 'wonderpush-android-sdk'
project.version = "4.1.6-SNAPSHOT"

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 21
        consumerProguardFiles 'proguard-exported.pro'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        buildConfigField 'String', 'WONDERPUSH_SDK_VERSION', '"' + project.version + '"'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'

    resourcePrefix 'wonderpush_android_sdk_'
}

dependencies {
    // This is how we can declare incompatibility with previous modules.
    // This way a newer SDK version can say it is not compatible with
    // previous module versions, whereas the module version can only
    // declare a minimal SDK version.
    // Failing to publish these constraints would allow an older version
    // of the module to accept the newer although incompatible SDK version.
    //constraints {
    //    implementation('com.wonderpush:wonderpush-android-sdk-fcm:[2.0,3.0)') {
    //        because 'ABI changes'
    //    }
    //}

    implementation 'androidx.core:core:1.0.0' // Note: this dependency is already depended on by at least com.google.firebase:firebase-messaging:20.1.2
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0' // Note: this dependency is already depended on by at least com.google.firebase:firebase-messaging:20.1.2
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.dagger:dagger:2.24'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.24'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    implementation 'javax.inject:javax.inject:1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.14'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation "com.squareup.picasso:picasso:2.8"
    implementation "com.squareup.okhttp3:okhttp:3.14.9"
    // necessary to bump dependency from picasso

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.json:json:20160810'
    testImplementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
}

if (file('local.gradle').exists()) {
    apply from: 'local.gradle'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Xdiags:verbose"
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    publications {
        release(MavenPublication) {
            afterEvaluate {
                from components.release // identical to artifact bundleReleaseAar
                artifact androidSourcesJar
                artifact androidJavadocsJar
            }
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version
            pom {
                packaging = 'aar'
                groupId = project.group
                artifactId = 'wonderpush-android-sdk'

                name = 'WonderPush Android SDK'
                description = 'WonderPush Android SDK'
                url = 'https://github.com/wonderpush/wonderpush-android-sdk'

                scm {
                    connection = 'scm:git@github.com:wonderpush/wonderpush-android-sdk.git'
                    developerConnection = 'scm:git@github.com:wonderpush/wonderpush-android-sdk.git'
                    url = 'https://github.com/wonderpush/wonderpush-android-sdk'
                }

                licenses {
                    license {
                        name = 'Apache 2.0'
                        url = 'https://github.com/wonderpush/wonderpush-android-sdk/blob/master/LICENSE.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'wonderpush'
                        name = 'WonderPush'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'wonderpush-android-sdk-repository'
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                if (project.hasProperty('wonderpush_publishing_repositories_maven_credentials_username')) {
                    username = wonderpush_publishing_repositories_maven_credentials_username
                }
                if (project.hasProperty('wonderpush_publishing_repositories_maven_credentials_password')) {
                    password = wonderpush_publishing_repositories_maven_credentials_password
                }
            }
        }
    }
}

afterEvaluate {
    signing {
        useGpgCmd()
        sign publishing.publications
        sign configurations.archives
    }
}

task androidJavadocs(type: Javadoc) {
    title = 'WonderPush Android SDK Reference'
    source = [
            'src/main/java/com/wonderpush/sdk/DeepLinkEvent.java',
            'src/main/java/com/wonderpush/sdk/WonderPush.java',
            'src/main/java/com/wonderpush/sdk/WonderPushAbstractDelegate.java',
            'src/main/java/com/wonderpush/sdk/WonderPushChannel.java',
            'src/main/java/com/wonderpush/sdk/WonderPushChannelGroup.java',
            'src/main/java/com/wonderpush/sdk/WonderPushDelegate.java',
            'src/main/java/com/wonderpush/sdk/WonderPushFirebaseMessagingService.java',
            'src/main/java/com/wonderpush/sdk/WonderPushUserPreferences.java',
            'src/main/java/com/wonderpush/sdk/WonderPushInitializer.java',
            'src/main/java/com/wonderpush/sdk/inappmessaging/InAppMessagingImpressionListener.java',
            'src/main/java/com/wonderpush/sdk/inappmessaging/InAppMessagingClickListener.java',
            'src/main/java/com/wonderpush/sdk/inappmessaging/model/Text.java',
            'src/main/java/com/wonderpush/sdk/inappmessaging/model/ModalMessage.java',
            'src/main/java/com/wonderpush/sdk/inappmessaging/model/Button.java',
            'src/main/java/com/wonderpush/sdk/inappmessaging/model/BannerMessage.java',
            'src/main/java/com/wonderpush/sdk/inappmessaging/model/CardMessage.java',
            'src/main/java/com/wonderpush/sdk/inappmessaging/model/InAppMessage.java',
            'src/main/java/com/wonderpush/sdk/inappmessaging/model/ImageOnlyMessage.java',
            'src/main/java/com/wonderpush/sdk/inappmessaging/model/MessageType.java',
            'src/main/java/com/wonderpush/sdk/inappmessaging/InAppMessagingDisplayErrorListener.java',
            'src/main/java/com/wonderpush/sdk/inappmessaging/InAppMessaging.java',
            'src/main/java/com/wonderpush/sdk/inappmessaging/InAppMessagingDisplayCallbacks.java',
            'src/main/java/com/wonderpush/sdk/inappmessaging/InAppMessagingDisplay.java'
    ] //android.sourceSets.main.java.srcDirs
    doFirst {
        classpath = project.files(
                project.android.libraryVariants.toList().first().javaCompileProvider.get().classpath.files,
                project.android.getBootClasspath(),
                "$projectDir/build/generated/ap_generated_sources/release/out",
                "$projectDir/build/generated/ap_generated_sources/debug/out",
                "$projectDir/build/intermediates/javac/release/compileReleaseJavaWithJavac/classes",
                "$projectDir/build/intermediates/javac/debug/compileDebugJavaWithJavac/classes",
                android.sourceSets.main.java.srcDirs
        )
    }
    options.overview = project.rootDir.path + '/overview.html'
    options.showFromPublic()
    // JDK 1.8 is more strict then 1.7. Have JDK 1.8 behave like 1.7 for javadoc generation
    options.addStringOption('Xdoclint:none', '-quiet')
}
project.afterEvaluate {
    androidJavadocs.dependsOn(assembleDebug)
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}
